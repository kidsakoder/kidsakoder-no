buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "http://repo.enonic.com/public"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.enonic.xp:gradle-plugin:${xpVersion}"
        classpath "com.moowork.gradle:gradle-node-plugin:0.11"
    }
}

apply plugin: 'com.enonic.xp.app'
apply plugin: 'com.moowork.node'

def frontendDir = "./src/frontend/"

app {
    name = project.appName
    displayName = project.displayName
    vendorName = project.vendorName
    vendorUrl = project.vendorUrl

    // Point xpHome to docker container so deploy knows where to put the jar.
    xpHome = new File("../enonic-server/exp/")
}

sourceSets {
    main {
        resources {
            srcDir 'site'
        }
    }
}

// ##
// ## Application tasks
// ##
task deployFull {
    mustRunAfter('npmBuild')
    dependsOn('npmBuild')
    dependsOn(':deploy')
    group 'application'
    description 'Builds and deploys entire application, including frontend npm build'
}

// ##
// ## Enonic XP Server tasks
// ##
task startserver(group: 'enonic xp', type: Exec) {
    workingDir "$projectDir/../"
    commandLine 'sh', 'exp', 'run'
}

task stopserver(group: 'enonic xp', type: Exec) {
    workingDir "$projectDir/../"
    commandLine 'sh', 'exp', 'stop'
}

task redeploy(group: 'enonic xp', type: Exec) {
    workingDir "$projectDir/../"
    commandLine 'sh', 'exp', 'redeploy'
}

// ##
// ## Frontend/npm tasks
// ##
task npmInstallDeps(type: NpmTask) {
    args = ['install']
    workingDir = frontendDir
    group 'node'
    description 'Install npm dependencies (package.json)'
}

task npmCleanDeps(group: 'node', description: 'Delete npm dependencies (**/node_modules/') << {
    def nodeModulesDir = new File("$frontendDir/node_modules")
    nodeModulesDir.deleteDir()
}

task npmCleanProject(type: NpmTask) {
    args = ['run', 'clean']
    workingDir = frontendDir
    group 'node'
    description 'Clean npm frontend project (package.json)'
}

task npmBuild(type: NpmTask) {
    args = ['run', 'build']
    workingDir = frontendDir
    group 'node'
    description 'Build npm frontend project (package.json)'
    dependsOn npmInstallDeps
}

clean.dependsOn(npmCleanProject)

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-thymeleaf:${xpVersion}"
    include "com.enonic.xp:lib-http-client:${xpVersion}"
    include "com.enonic.xp:lib-mail:${xpVersion}"
    include "com.enonic.xp:lib-i18n:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-cache:${xpVersion}"
    include "com.enonic.lib:menu:1.2.0"
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://repo.enonic.com/public'
    }
}

node {
    // Version of node to use.
    version = '4.2.3'

    // Version of npm to use.
    npmVersion = '2.14.7'

    // Base URL for fetching node distributions (change if you have a mirror).
    //distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    //workDir = file("${project.buildDir}/nodejs")
    //workDir = file("${project.projectDir}/src/resources/frontend/node_modules/nodejs/")

    // Set the work directory where node_modules should be located
    //nodeModulesDir = file("${project.projectDir}/src/resources/frontend/")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

